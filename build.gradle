// Top-level build file where you can add configuration options common to all sub-projects/modules.
def getLocalProperty(String key) {
    if (!localPropertiesFile().exists()) {
        return null
    }

    Properties properties = new Properties()
    properties.load(localPropertiesFile().newDataInputStream())
    return properties.getProperty(key)
}

def localPropertiesFile() {
    return project.rootProject.file('local.properties');
}

def isForUpload2Maven() {
    return getLocalProperty("for.upload") == "true"
}

//获取当前时间
def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

buildscript {
    repositories {
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0-beta4'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
        File file = project.rootProject.file('local.properties')
        if (file.exists()) {
            Properties properties = new Properties()
            properties.load(file.newDataInputStream())
            boolean upload2maven = properties.getProperty("for.upload") == "true"
            if (upload2maven) {
                println ':global:include upload 2 maven classpath'
                classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.4'
                classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
                classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.13.1"
            }
        }
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
